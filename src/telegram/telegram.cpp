#include <Arduino.h>
#include <WiFiClientSecure.h>
#include <WiFi.h>
#include <UniversalTelegramBot.h>
#include "config/pins.h"
#include "drivers/pumps/pump_station/pump_station_control.h"

// [TELEGRAM]

char ADMIN_CHAT_ID[32] = "396017793";

static const char *BOT_TOKEN = "8570607408:AAGrdnY5JCkopb1oYP6TjfXLYdBJiewS7Dg";
WiFiClientSecure client;
UniversalTelegramBot bot(BOT_TOKEN, client);

static bool initSendingMessage = false;
static bool sendMessageAfterStartWellPump = false;

static const unsigned int pollingBotDelay = 6000;
static unsigned long botLastTime;

enum TelegramCommand
{
   CMD_STATUS,
   CMD_PUMP_ON,
   CMD_PUMP_OFF,
   CMD_UNKNOWN
};

// Telegram –æ—á–µ—Ä–µ–¥—å —Å–æ–æ–±—â–µ–Ω–∏–π
struct TgMessage
{
   char chatId[32];
   String text;
   uint8_t retries;
};

constexpr uint8_t TG_QUEUE_SIZE = 10;
TgMessage tgQueue[TG_QUEUE_SIZE];
uint8_t queueHead = 0;
uint8_t queueTail = 0;
uint8_t queueCount = 0;
const uint8_t NUMBER_ATTEMPTS = 3;

void enqueueMessage(const char chat_id[32], const String &text)
{
   if (queueCount < TG_QUEUE_SIZE)
   {
      TgMessage &msg = tgQueue[queueTail];

      strncpy(msg.chatId, chat_id, sizeof(msg.chatId));
      msg.chatId[sizeof(msg.chatId) - 1] = '\0';
      tgQueue[queueTail].text = text;
      tgQueue[queueTail].retries = 0;
      queueTail = (queueTail + 1) % TG_QUEUE_SIZE;
      queueCount++;
   }
   else
   {
      Serial.println("–û—á–µ—Ä–µ–¥—å —Å–æ–æ–±—â–µ–Ω–∏–π –ø–µ—Ä–µ–ø–æ–ª–Ω–µ–Ω–∞!");
   }
}

void processQueue()
{
   if (queueCount == 0)
      return;

   TgMessage &msg = tgQueue[queueHead];
   if (bot.sendMessage(msg.chatId, msg.text))
   {
      queueHead = (queueHead + 1) % TG_QUEUE_SIZE;
      queueCount--;
      Serial.println("–°–æ–æ–±—â–µ–Ω–∏–µ –¥–æ—Å—Ç–∞–≤–ª–µ–Ω–æ");
   }
   else
   {
      if (++msg.retries > NUMBER_ATTEMPTS)
      {
         queueHead = (queueHead + 1) % TG_QUEUE_SIZE;
         queueCount--;
         Serial.println("–°–æ–æ–±—â–µ–Ω–∏–µ –Ω–µ –¥–æ—Å—Ç–∞–≤–ª–µ–Ω–æ –∏ —É–¥–∞–ª–µ–Ω–æ –ø–æ—Å–ª–µ n –ø–æ–ø—ã—Ç–æ–∫");
      }
   }
}

// –ü–∞—Ä—Å–∏–Ω–≥ —Ç–µ–ª–µ–≥—Ä–∞–º –∫–æ–º–∞–Ω–¥ –¥–ª—è switch/case
TelegramCommand parseCommand(const String &cmd)
{
   if (cmd == "status" || cmd == "/status")
      return CMD_STATUS;
   if (cmd == "pumpon" || cmd == "/pumpon")
      return CMD_PUMP_ON;
   if (cmd == "pumpoff" || cmd == "/pumpoff")
      return CMD_PUMP_OFF;
   return CMD_UNKNOWN;
}

String messageWithTime(String mes)
{
   return String(millis()) + ": " + mes;
}

void handleCommand(TelegramCommand cmd, char chatId[32])
{
   switch (cmd)
   {
   case CMD_STATUS:

      Serial.println("üí° LED –≤–∫–ª—é—á–µ–Ω!");
      break;

   case CMD_PUMP_ON:
      digitalWrite(PIN_PUMP_STATION, HIGH);
      enqueueMessage(chatId, messageWithTime("–í–∫–ª—é—á–µ–Ω–æ"));
      Serial.println("üåë–¢–æ–∫ –Ω–∞ 13 –ø–æ—Ä—Ç—É!");
      break;
   case CMD_PUMP_OFF:
      digitalWrite(PIN_PUMP_STATION, LOW);
      enqueueMessage(chatId, messageWithTime("–í—ã–∫–ª—é—á–µ–Ω–æ"));
      Serial.println("–¢–æ–∫ –Ω–∞ 13 –ø–æ—Ä—Ç—É –≤—ã–∫–ª—é—á–µ–Ω!");
      break;
   case CMD_UNKNOWN:
   default:
      Serial.println("‚ùå –ù–µ–∏–∑–≤–µ—Å—Ç–Ω–∞—è –∫–æ–º–∞–Ω–¥–∞!");
      break;
   }
}

void handleNewMessages(int numNewMessages)
{
   for (int i = 0; i < numNewMessages; i++)
   {
      Serial.print("–ö–æ–ª-–≤–æ —Å–æ–æ–±—â–µ–Ω–∏–π: ");
      Serial.println(numNewMessages);
      char chatId[32]; // –≤—ã–±–∏—Ä–∞–µ–º —Ä–∞–∑–º–µ—Ä, –∫–æ—Ç–æ—Ä—ã–π —Ç–æ—á–Ω–æ –≤–ª–µ–∑–µ—Ç (Telegram chat_id –æ–±—ã—á–Ω–æ –¥–æ 20 —Å–∏–º–≤–æ–ª–æ–≤)
      strncpy(chatId, bot.messages[i].chat_id.c_str(), sizeof(chatId));
      chatId[sizeof(chatId) - 1] = '\0'; // –±–µ–∑–æ–ø–∞—Å–Ω—ã–π null-—Ç–µ—Ä–º–∏–Ω–∞—Ç–æ—Ä
      String text = bot.messages[i].text;
      String fromName = bot.messages[i].from_name;
      const TelegramCommand cmd = parseCommand(text);
      handleCommand(cmd, chatId);
      Serial.print("chatId");
      Serial.println(chatId);
   }
}

void telegramCheckAndSend()
{
   if (pendingWater && !sendMessageAfterStartWellPump)
   {
      processQueue();
      sendMessageAfterStartWellPump = true;
      return;
   }
   if (WiFi.status() == WL_CONNECTED && millis() - botLastTime > pollingBotDelay)
   {
      if (sendMessageAfterStartWellPump == true)
         sendMessageAfterStartWellPump = !sendMessageAfterStartWellPump;

      if (!initSendingMessage)
      {
         enqueueMessage(ADMIN_CHAT_ID, "ESP32 '–ù–∞—Å–æ—Å–Ω–∞—è —Å—Ç–∞–Ω—Ü–∏—è' –ø–µ—Ä–µ–∑–∞–ø—É—Å—Ç–∏–ª–∞—Å—å, –≤ —Å–µ—Ç–∏ –∏ –ø—Ä–∏—Å—Ç—É–ø–∏–ª–∞ –∫ —Ä–∞–±–æ—Ç–µ");
         initSendingMessage = !initSendingMessage;
      };
      int numNewMessages = bot.getUpdates(bot.last_message_received + 1);
      if (numNewMessages > 0)
      {
         handleNewMessages(numNewMessages);
      }
      botLastTime = millis();
      processQueue();
   }
}

void initTelegram()
{
   client.setInsecure();
   bot.waitForResponse = 4000;
}
